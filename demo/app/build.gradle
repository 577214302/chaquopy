apply plugin: 'com.android.application'
apply plugin: 'com.chaquo.python'  // Must come after com.android.application

// *** Remove in public repository ********************************************
import com.chaquo.python.Common;

// TODO #5203 use sourceSets, and remove .gitignore entries
def IN_DIR = "../../product/runtime/src/test/python"
def OUT_DIR = "src/main/python"
// This list should be the same as the one in .gitignore.
for (filename in ["chaquopy/test", "package1", "module1.py"]) {
    assert file("$IN_DIR/$filename").exists()
    delete "$OUT_DIR/$filename"
    copy {
        from IN_DIR
        include filename, "$filename/**"
        into OUT_DIR
    }
}
// **********************************************************************

afterEvaluate {
    def assetsSrcDir = "src/main/assets/source"
    delete assetsSrcDir
    mkdir assetsSrcDir
    for (filename in ["src/main/python/chaquopy/demo/ui_demo.py",
                      "src/main/java/com/chaquo/python/demo/JavaDemoActivity.java"]) {
        assert file(filename).exists()
        copy {
            from filename
            into assetsSrcDir
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.chaquo.python.demo"
        minSdkVersion 15
        targetSdkVersion 23

        // *** Replace in public repository **********************************
        def ver = file("../../VERSION.txt").text.trim()
        versionName ver + "-SNAPSHOT"

        // Final digit is 0 for -SNAPSHOT builds in this repo, and 1 for released builds in the
        // public repo.
        def verParsed = ver.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 0)

        ndk {
            abiFilters Common.ABIS.toArray()
        }
        // **********************************************************************

        python {
            version "2.7.10"

            // Enable and edit the following line if "python2" is not on your PATH.
            // buildPython "C:/Python27/python.exe"

            // Android UI demo
            pip {
                install "Pygments==2.2.0"   // Also used in Java API demo
            }
            staticProxy "chaquopy.demo.ui_demo"

            // Python unit tests
            pip {
                install "MarkupSafe==1.0"
            }
            staticProxy("chaquopy.test.static_proxy.basic", "chaquopy.test.static_proxy.header",
                        "chaquopy.test.static_proxy.method")
        }

        // Chaquopy generates extra internal-use constructors on static proxy classes.
        lintOptions {
            disable "ValidFragment"
        }

        // Remove other languages imported from Android support libraries.
        resConfigs "en"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // *** Remove in public repository ***************************************
    signingConfigs {
        config {
            storeFile file('../chaquo.jks')
            keyAlias 'key0'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        release { signingConfig signingConfigs.config }
        debug { signingConfig signingConfigs.config }
    }

    sourceSets {
        main {
            java.srcDirs += ['../../product/runtime/src/test/java']
        }
    }
    // **********************************************************************
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:preference-v14:23.4.0'
    compile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
}
