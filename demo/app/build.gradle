apply plugin: 'com.android.application'
apply plugin: 'com.chaquo.python'  // Must come after com.android.application

// *** Remove in public repository ********************************************
import com.chaquo.python.Common;
// **********************************************************************

afterEvaluate {
    def assetsSrcDir = "src/main/assets/source"
    delete assetsSrcDir
    mkdir assetsSrcDir
    for (filename in ["src/main/python/chaquopy/demo/ui_demo.py",
                      "src/main/java/com/chaquo/python/demo/JavaDemoActivity.java"]) {
        assert file(filename).exists()
        copy {
            from filename
            into assetsSrcDir
        }
    }
}

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26

        // *** Replace in public repository **********************************
        // Final digit of versionCode is 0 for -SNAPSHOT builds in this repo, and 1 for released
        // builds in the public repo.
        versionName rootProject.version + "-SNAPSHOT"
        def verParsed = rootProject.version.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 0)

        ndk {
            abiFilters Common.ABIS.toArray()
        }
        // **********************************************************************

        python {
            // Enable and edit the following line if "python" is not on your PATH.
            // buildPython "C:/Python27/python.exe"

            // Android UI demo
            pip {
                install "Pygments==2.2.0"   // Also used in Java API demo
            }
            staticProxy "chaquopy.demo.ui_demo"

            // Python unit tests
            pip {
                install "MarkupSafe==1.0"
                install "certifi==2018.01.18"  // This is in the default extractPackages list.
            }
            staticProxy("chaquopy.test.static_proxy.basic", "chaquopy.test.static_proxy.header",
                        "chaquopy.test.static_proxy.method")
        }

        // Chaquopy generates extra internal-use constructors on static proxy classes.
        lintOptions {
            disable "ValidFragment"
        }

        // Remove other languages imported from Android support libraries.
        resConfigs "en"
    }

    flavorDimensions "pyVersion"
    productFlavors {
        // Allow temporary single-version testing just by editing Common.java.
        if (Common.PYTHON_VERSIONS.contains("2.7.15")) {
            py2 {
                dimension "pyVersion"
                applicationId "com.chaquo.python.demo"
                python { version "2.7.15" }
            }
        }
        if (Common.PYTHON_VERSIONS.contains("3.6.5")) {
            py3 {
                dimension "pyVersion"
                applicationId "com.chaquo.python.demo3"
                python { version "3.6.5" }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // *** Remove in public repository ***************************************
    signingConfigs {
        config {
            storeFile file('../chaquo.jks')
            keyAlias 'key0'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        release { signingConfig signingConfigs.config }
        debug { signingConfig signingConfigs.config }
    }

    sourceSets {
        def testSrc = "../../product/runtime/src/test"
        main {
            java { srcDir "$testSrc/java" }
            python { srcDir "$testSrc/python" }
        }
    }
    // **********************************************************************
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "android.arch.lifecycle:extensions:1.1.0"
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:preference-v14:26.1.0'
    implementation 'junit:junit:4.12'
    testImplementation 'junit:junit:4.12'
    testImplementation 'android.arch.core:core-testing:1.1.0'
}
