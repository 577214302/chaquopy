apply plugin: 'com.android.application'
apply plugin: 'com.chaquo.python'  // Must come after com.android.application

// *** Remove in public repository ********************************************
import com.chaquo.python.Common;

// TODO #5203 use sourceSets, and remove .gitignore entries
afterEvaluate {
    task("copyPythonSrc", type: Copy) {
        from "../../product/runtime/src/test/python"
        into "src/main/python"
        include "**/*.py"
    }
    generateDebugPythonSources.dependsOn(copyPythonSrc)
    generateDebugPythonAssets.dependsOn(copyPythonSrc)
}
// **********************************************************************

afterEvaluate {
    def assetsSrcDir = "src/main/assets/source"
    delete assetsSrcDir
    mkdir assetsSrcDir
    for (filename in ["src/main/python/com/chaquo/python/demo/ui_demo.py",
                      "src/main/java/com/chaquo/python/demo/JavaDemoActivity.java"]) {
        copy {
            from filename
            into assetsSrcDir
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.chaquo.python.demo"
        minSdkVersion 15
        targetSdkVersion 23

        // *** Replace in public repository **********************************
        def ver = file("../../VERSION.txt").text.trim()
        versionName ver + "-SNAPSHOT"

        // Final digit is 0 for -SNAPSHOT builds in this repo, and 1 for released builds in the
        // public repo.
        def verParsed = ver.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 0)

        ndk {
            abiFilters Common.ABIS.toArray()
        }
        // **********************************************************************

        python {
            // Enable and edit the following line if "python" is not on your PATH.
            // buildPython "C:/Python36/python.exe"
            version "2.7.10"
            pipInstall "--platform", "android_15_x86"   // FIXME
            pipInstall "Pygments==2.2.0"
            pipInstall "MarkupSafe==1.0"
            staticProxy "com.chaquo.python.demo.ui_demo"
            staticProxy "static_proxy.basic", "static_proxy.header", "static_proxy.method"
        }
        resConfigs "en"
        lintOptions {
            // Chaquopy generates extra internal-use constructors on static proxy classes.
            disable "ValidFragment"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // *** Remove in public repository ***************************************
    signingConfigs {
        config {
            storeFile file('../chaquo.jks')
            keyAlias 'key0'
            keyPassword 'android'
            storePassword 'android'
        }
    }
    buildTypes {
        release { signingConfig signingConfigs.config }
        debug { signingConfig signingConfigs.config }
    }

    sourceSets {
        main {
            java.srcDirs += ['../../product/runtime/src/test/java']
        }
    }
    // **********************************************************************
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:preference-v14:23.4.0'
    compile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
}
