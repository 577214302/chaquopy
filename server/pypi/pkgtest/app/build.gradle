apply plugin: 'com.android.application'

apply plugin: 'com.chaquo.python'
import com.chaquo.python.Common


android {
    compileSdkVersion 23
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23

        // Final digit of versionCode is 0 for -SNAPSHOT builds in this repo, and 1 for released
        // builds in the public repo.
        versionName rootProject.version + "-SNAPSHOT"
        def verParsed = rootProject.version.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 0)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        python {
            version "2.7.10"
        }
        ndk {
            abiFilters Common.ABIS.toArray()
        }
    }

    flavorDimensions "pyVersion"
    productFlavors {
        py2 {
            dimension "pyVersion"
            applicationId "com.chaquo.python.pkgtest2"
            python { version "2.7.10" }
        }
        py3 {
            dimension "pyVersion"
            applicationId "com.chaquo.python.pkgtest3"
            python { version "3.6.3" }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def demoSrc = "../../../../demo/app/src"
        main {
            java {
                srcDirs = ["$demoSrc/main/java"]
                includes = ["**/App.java", "**/ConsoleActivity.java", "**/MainActivity.java",
                            "**/UnitTestActivity.java", "**/PythonTestActivity.java",
                            "**/ReplActivity.java"]
            }
            res { srcDirs = ["$demoSrc/main/res"] }
        }
        py2.res { srcDir "$demoSrc/py2/res" }
        py3.res { srcDir "$demoSrc/py3/res" }
    }
}


delete("src/main/python")
copy {
    from "../../../../demo/app/src/main/python"
    include "chaquopy/__init__.py", "chaquopy/demo/**"
    into "src/main/python"
}

def PY_INPUT_DIR = "../../packages"
def PY_OUTPUT_DIR = "src/main/python/chaquopy/test"
def INCLUDE_PACKAGES = []
def EXCLUDE_PACKAGES = []
String suiteSrc = ""
for (def inDir : file(PY_INPUT_DIR).listFiles().findAll { it.isDirectory() }) {
    def pkgName = inDir.name
    if ((!INCLUDE_PACKAGES.isEmpty() && !INCLUDE_PACKAGES.contains(pkgName)) ||
        EXCLUDE_PACKAGES.contains(pkgName)) {
        continue
    }

    def pkgVer = file("$inDir/version.txt").text.trim()
    android.defaultConfig.python.pip { install "$pkgName==$pkgVer" }
    def inFilename = "$inDir/test.py"
    assert file(inFilename).exists()
    copy {
        from inFilename
        into PY_OUTPUT_DIR
        rename "test.py", "${pkgName}.py"
    }
    suiteSrc += "from .${pkgName} import *\n"
}
file("$PY_OUTPUT_DIR/__init__.py").text = suiteSrc


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:preference-v14:23.4.0'
    testImplementation 'junit:junit:4.12'
}
